plugins {
    id 'com.android.application' version '8.4.0'
}

repositories {
    google()
    mavenCentral()
}

android {
    namespace = 'com.local.phpserver'
    compileSdk = 34

    def gitVersionCode = {
        "git rev-list --count HEAD".execute().text.trim().toInteger()
    }

    defaultConfig {
        applicationId "com.local.phpserver"
        minSdk = 24
        targetSdk = 34
        versionCode gitVersionCode()
        versionName "1.0.${versionCode}"
        
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    // Настройка упаковки (Замена устаревшему extractNativeLibs)
    packagingOptions {
        jniLibs {
            // Эта опция заставляет Android распаковать libphp.so при установке
            useLegacyPackaging = true
        }
    }

    // Наша плоская структура папок
    sourceSets {
        main {
            manifest.srcFile 'src/AndroidManifest.xml'
            java.srcDirs = ['src/java']
            res.srcDirs = ['src/res']
            jniLibs.srcDirs = ['src/jniLibs']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.webkit:webkit:1.9.0'
}
